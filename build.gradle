buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessPluginVersion"
		classpath "de.thetaphi:forbiddenapis:2.5"
		classpath "de.aaschmid:gradle-cpd-plugin:1.0"
	}
}

plugins {
	id 'java-library'
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'com.jfrog.bintray' version '1.8.4'
	id 'maven-publish'
	id 'com.github.spotbugs' version '1.6.4' apply false
}

// basic plugins
apply plugin: 'io.spring.dependency-management'

// code quality plugins
apply plugin: "checkstyle"
apply plugin: "pmd"
apply plugin: "cpd"
apply plugin: "jacoco"
apply plugin: "de.thetaphi.forbiddenapis"
apply plugin: "com.diffplug.gradle.spotless"

// code quality configuration
apply from: "${rootProject.projectDir}/config/checkstyle.gradle"
apply from: "${rootProject.projectDir}/config/spotbugs.gradle"
apply from: "${rootProject.projectDir}/config/pmd.gradle"
apply from: "${rootProject.projectDir}/config/cpd.gradle"
apply from: "${rootProject.projectDir}/config/jacoco.gradle"
apply from: "${rootProject.projectDir}/config/forbiddenapis.gradle"
apply from: "${rootProject.projectDir}/config/spotless.gradle"

// custom configuration
apply from: "${rootProject.projectDir}/gradle/version.gradle"
apply from: "${rootProject.projectDir}/gradle/sourceArtifact.gradle"
apply from: "${rootProject.projectDir}/gradle/bintray.gradle"

group = 'org.ws2ten1'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'org.springframework.data:spring-data-commons'
	compileOnly 'org.projectlombok:lombok'
	compileOnly 'com.google.code.findbugs:annotations:3.0.1'
	testCompileOnly 'org.projectlombok:lombok'
	testCompileOnly 'com.google.code.findbugs:annotations:3.0.1'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
	reports {
		html {
			enabled = true
		}
		xml {
			enabled = false
		}
	}
}
